apply plugin: 'java'
apply plugin: 'eclipse'
apply plugin: 'idea'
apply plugin: 'spring-boot'

dependencies {
    compile('org.springframework.boot:spring-boot-starter-web') {
        exclude module: "spring-boot-starter-tomcat"
    }
    compile 'org.springframework.boot:spring-boot-starter-jetty'

    // Web UI
    compile 'org.springframework.boot:spring-boot-starter-thymeleaf'

    // Twitter
    compile 'org.springframework.social:spring-social-twitter'

    // Integration
    compile 'org.springframework.boot:spring-boot-starter-integration'

    // Distributed persistence
    compile 'org.springframework.boot:spring-boot-starter-redis'
}

apply plugin: 'maven-publish'
apply plugin: 'com.jfrog.bintray'

publishing {
    publications {
        mavenJava(MavenPublication) {
            if (plugins.hasPlugin('war')) {
                from components.web
            } else {
                from components.java
            }
        }
    }
}

bintray {
    user            =   'webdizz'
    key             =   bintrayKey
    dryRun          =   false
    publish         =   true
    publications    =   ['mavenJava']
    pkg {
        repo = 'maven'
        name = 'bootiful-apps'
        desc = 'Bootiful App for IT Week@EPAM 2014'
        websiteUrl = 'https://github.com/webdizz/bootiful-apps/'
        vcsUrl = 'https://github.com/webdizz/bootiful-apps.git'
        licenses = ['Apache-2.0']
        labels = ['Spring Boot', 'Redis', 'Twitter', 'Spring Integration']
        publicDownloadNumbers = true
    }
}

bintrayUpload.dependsOn 'bootRepackage'

processResources {
    into("/") {
        from(file("${project.getRootProject().getRootDir()}/application.yml"))
    }
}